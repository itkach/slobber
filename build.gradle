plugins {
    id 'java-library'
}

def getVersionName = {
    return "git -C ${projectDir} describe --tags".execute().text.trim()
}

version = getVersionName()

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
}

def manifestAttributes = [
        "Main-Class": "itkach.slobber.Slobber",
        "Implementation-Title": "Slobber",
        "Implementation-Version": version
]

jar {
    manifest {
        attributes manifestAttributes
    }
}


tasks.register('uberJar', Jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from sourceSets.main.output

    manifest {
        attributes manifestAttributes
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    def excludeList = project(":slobj").file("exclude.txt")
    println("Excluding files listed in " + excludeList.path)
    excludeList.eachLine {
        line ->
            if (!line.startsWith("#")) {
                exclude line.trim()
            }
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.4.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.4.2'
    api files('libs/simple-5.1.7.1.itkach.jar')
    api project(':slobj')
 }
